plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}


group = 'fr.Mathildeuh'
version = '2.0.0'

def mcVersion = '1.21.10-R0.1-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name = "spigot-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://jitpack.io/"
    }
    maven {
        url = "https://repo.codemc.io/repository/maven-snapshots/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    // Paper maven (left for compatibility)
    maven {
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
}

dependencies {
    // Target Spigot API for 1.21.10 (switch from Paper back to Spigot)
    compileOnly "org.spigotmc:spigot-api:${mcVersion}"
    implementation "org.bstats:bstats-bukkit:3.0.2"
    implementation "net.kyori:adventure-text-minimessage:4.17.0"
    implementation "net.kyori:adventure-platform-bukkit:4.3.2"

    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.14'
    implementation 'org.zeroturnaround:zt-zip:1.17'

    implementation 'org.json:json:20231013'
    implementation 'commons-io:commons-io:2.11.0'

    compileOnly 'me.clip:placeholderapi:2.11.6'

    // JetBrains annotations for nullability hints (compileOnly)
    compileOnly 'org.jetbrains:annotations:24.0.1'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

compileJava.options.encoding("UTF-8")

shadowJar {
    archiveClassifier.set('')
    relocate 'org.bstats', 'fr.mathildeuh.bstats'
    relocate 'com.github.stefvanschie.inventoryframework', 'fr.mathildeuh.inventoryframework'

    doLast {
        def pluginDir = file("C:\\Users\\Mathilde\\IdeaProjects\\WorldManager\\server\\plugins")
        if (!pluginDir.exists()) {
            pluginDir.mkdirs()
        }
        def jarFile = jar.archiveFile.get().asFile
        copy {
            from jarFile
            into pluginDir
        }
        println "âœ“ Plugin copied to server/plugins/${jarFile.name}"
    }
}

